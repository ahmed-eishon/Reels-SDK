plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    namespace 'com.rakuten.room.reels'
    compileSdk Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdk Integer.parseInt(project.MIN_SDK_VERSION)
        targetSdk Integer.parseInt(project.TARGET_SDK_VERSION)
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        
        qa {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        
        qaNoObfuscation {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        
        eap {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url 'https://storage.googleapis.com/download.flutter.io'
    }
}

dependencies {
    // Flutter module - using api instead of implementation to expose to consuming apps
    debugApi 'com.example.reels_flutter:flutter_debug:1.0'
    releaseApi 'com.example.reels_flutter:flutter_release:1.0'

    // Flutter engine embedding - required for Flutter runtime
    debugApi 'io.flutter:flutter_embedding_debug:1.0.0-b45fa18946'
    releaseApi 'io.flutter:flutter_embedding_release:1.0.0-b45fa18946'

    // Architecture-specific Flutter engine artifacts
    debugApi 'io.flutter:armeabi_v7a_debug:1.0.0-b45fa18946'
    debugApi 'io.flutter:arm64_v8a_debug:1.0.0-b45fa18946'
    debugApi 'io.flutter:x86_64_debug:1.0.0-b45fa18946'
    releaseApi 'io.flutter:armeabi_v7a_release:1.0.0-b45fa18946'
    releaseApi 'io.flutter:arm64_v8a_release:1.0.0-b45fa18946'
    releaseApi 'io.flutter:x86_64_release:1.0.0-b45fa18946'

    // Media3 dependencies required by video_player plugin
    api 'androidx.media3:media3-exoplayer:1.1.1'
    api 'androidx.media3:media3-common:1.1.1'
    api 'androidx.media3:media3-datasource:1.1.1'

    // ReLinker required by Flutter embedding
    api 'com.getkeepsafe.relinker:relinker:1.4.5'

    // Android X
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.fragment.ktx)
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.google.material.design)

    // Jetbrains
    implementation(libs.jetbrains.kotlin.stdlib)

    // Testing
    testImplementation(libs.junit4)
    androidTestImplementation(libs.androidx.test.junit)
    androidTestImplementation(libs.androidx.test.espresso)
}

// Maven Publishing Configuration
ext.getVersionName = { ->
    def versionFile = new File(projectDir.parentFile, 'VERSION')
    return versionFile.exists() ? versionFile.text.trim() : '1.0.0'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.ahmed-eishon'
            artifactId = 'reels-sdk'
            version = getVersionName()

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'Reels SDK for Android'
                description = 'Flutter-based video reels SDK for Android applications'
                url = 'https://github.com/ahmed-eishon/Reels-SDK'

                scm {
                    connection = 'scm:git:git://github.com/ahmed-eishon/Reels-SDK.git'
                    developerConnection = 'scm:git:ssh://github.com/ahmed-eishon/Reels-SDK.git'
                    url = 'https://github.com/ahmed-eishon/Reels-SDK'
                }
            }
        }

        debug(MavenPublication) {
            groupId = 'com.rakuten'
            artifactId = 'reels_android'
            version = getVersionName()

            afterEvaluate {
                from components.debug
            }

            pom {
                name = 'Reels SDK for Android - Debug'
                description = 'Flutter-based video reels SDK for Android applications (Debug build)'
                url = 'https://github.com/ahmed-eishon/Reels-SDK'
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahmed-eishon/Reels-SDK")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key") ?: ""
            }
        }
    }
}